// GHOST 

domain ResourceHolder {
    // Accounts 
    function ghostBalance(rh: ResourceHolder, resource: Resource): Ref

    axiom balanceInjective {
        forall owner1: ResourceHolder, owner2: ResourceHolder, resource1: Resource, resource2: Resource :: 
        owner1 != owner2 || resource1 != resource2 ==> ghostBalance(owner1, resource1) != ghostBalance(owner2, resource2)
    }

}

function total(resources: Set[ResourceHolder], resource: Resource): Int
    requires forall r : ResourceHolder :: acc(ghostBalance(r, resource).resourceAmount)

domain Resource {

}

domain ActionPerm {
    function canTransferOut(thePerm: ActionPerm, from: ResourceHolder, resource: Resource, amount: Int): Bool
    function canMint(thePerm: ActionPerm, to: ResourceHolder, resource: Resource, amount: Int): Bool
}

function canBurn(thePerm: ActionPerm, to: ResourceHolder, resource: Resource, amount: Int): Bool {
    canMint(thePerm, to, resource, 0 - amount)
}

// A permission to perform an action on a resource
field actionPerm: ActionPerm


// Field of returned ref from `balance`
field resourceAmount: Int

method ghostMint(
    to: ResourceHolder, 
    resource: Resource,
    amount: Int,
    permRef: Ref
) 
   requires acc(ghostBalance(to, resource).resourceAmount)
   requires acc(permRef.actionPerm)
   requires(ghostBalance(to, resource).resourceAmount + amount >= 0)
   ensures acc(ghostBalance(to, resource).resourceAmount)
   ensures ghostBalance(to, resource).resourceAmount == old(ghostBalance(to, resource).resourceAmount) + amount
{
    ghostBalance(to, resource).resourceAmount := ghostBalance(to, resource).resourceAmount + amount
}

method ghostTransfer(
    from: ResourceHolder, 
    to: ResourceHolder, 
    resource: Resource,
    amount: Int,
    permRef: Ref
)
   requires acc(ghostBalance(from, resource).resourceAmount)
   requires acc(ghostBalance(to, resource).resourceAmount)
   requires acc(permRef.actionPerm)
   requires(amount >= 0)
   requires(ghostBalance(from, resource).resourceAmount >= amount)
   requires canTransferOut(permRef.actionPerm, from, resource, amount)
   ensures acc(ghostBalance(from, resource).resourceAmount)
   ensures acc(ghostBalance(to, resource).resourceAmount)
   ensures ghostBalance(from, resource).resourceAmount == old(ghostBalance(from, resource).resourceAmount) - amount
   ensures ghostBalance(to, resource).resourceAmount == old(ghostBalance(to, resource).resourceAmount) + amount

   // Sanity check: total sum of resource unchanged
   ensures (ghostBalance(from, resource).resourceAmount + ghostBalance(to, resource).resourceAmount) ==
           old(ghostBalance(from, resource).resourceAmount + ghostBalance(to, resource).resourceAmount)

{
    ghostBalance(from, resource).resourceAmount := ghostBalance(from, resource).resourceAmount - amount
    ghostBalance(to, resource).resourceAmount := ghostBalance(to, resource).resourceAmount + amount
}


// NONGHOST

field bankBalances: Map[AccountID, Map[Coin, Map[Path, Int]]]

define hasCoin(map, acctID, c) (
    acctID in map && c in map[acctID]
)

define balancesPositive(map) (
    (forall acctID: AccountID, c: Coin, p: Path :: 
        (hasCoin(map, acctID, c) && p in map[acctID][c]) ==> map[acctID][c][p] >= 0)
)

define balanceOfM(map, acctID, c) (
    sumValues(map[acctID][c])
)

function balanceOf(map: Map[AccountID, Map[Coin, Map[Path, Int]]], acctID: AccountID, c: Coin): Int
    requires balancesPositive(map)
 {
    hasCoin(map, acctID, c) ? balanceOfM(map, acctID, c) : 0
 }

domain AccountID {
    function toResourceHolder(acctId: AccountID): ResourceHolder

    axiom injective {
        forall a1 : AccountID, a2: AccountID :: a1 != a2 ==> toResourceHolder(a1) != toResourceHolder(a2)
    }
}

domain Path {
    function startsWith(path: Path, port: Port, channel: Channel): Bool
    function prependPrefix(path: Path, port: Port, channel: Channel): Path
}

function dropPrefix(path: Path, port: Port, channel: Channel): Path
  requires(startsWith(path, port, channel))

// Monomorphised
function sumValues(map: Map[Path, Int]): Int
    requires forall p : Path :: p in map ==> map[p] >= 0
    ensures forall p : Path :: p in map ==> map[p] <= result
    ensures map == Map() ==> result == 0

domain Coin {
    function toResource(coin: Coin): Resource

    axiom toResourceInjective {
        forall c1 : Coin, c2: Coin :: c1 != c2 ==> toResource(c1) != toResource(c2)
    }

    function mkCoin(id: Int): Coin
}


define updateBalance(bank, acctID, path, coin, amt){
    if (!(acctID in bank.bankBalances)) {
        bank.bankBalances := bank.bankBalances[acctID := Map()]
    }
    var coinMap : Map[Coin, Map[Path, Int]]:= bank.bankBalances[acctID]
    if (!(coin in coinMap)) {
        coinMap := coinMap[coin := Map()]
    }
    var pathMap : Map[Path, Int] := coinMap[coin]
    if (!(path in pathMap)) {
       pathMap := pathMap[path := 0]
    }
    pathMap := pathMap[path := pathMap[path] + amt]
    assume sumValues(pathMap) == sumValues(coinMap[coin]) + amt
    coinMap := coinMap[coin := pathMap]
    bank.bankBalances := bank.bankBalances[acctID := coinMap]
}

define bankInvariants(bankBalances) (
    forall r : ResourceHolder,c : Coin :: acc(ghostBalance(r, toResource(c)).resourceAmount) &&
    balancesPositive(bankBalances) &&
    (forall acctID : AccountID, c : Coin ::
            balanceOf(bankBalances, acctID, c) == 
            ghostBalance(toResourceHolder(acctID), toResource(c)).resourceAmount
    )
)

define Bank(bank) (
    acc(bank.bankBalances) && bankInvariants(bank.bankBalances)
)

method bankMintTokens(
    bank: Ref,
    to: AccountID,
    path: Path,
    coin: Coin,
    amt: Int,
    ghostPerm: Ref
) returns (success: Bool)
    requires Bank(bank)
    requires acc(ghostPerm.actionPerm)
    requires amt >= 0
    requires canMint(ghostPerm.actionPerm, toResourceHolder(to), toResource(coin), amt)
    ensures Bank(bank)
    ensures balanceOf(bank.bankBalances, to, coin) == old(balanceOf(bank.bankBalances, to, coin) + amt)
{
    ghostMint(
        toResourceHolder(to),
        toResource(coin),
        amt,
        ghostPerm
    )
    updateBalance(bank, to, path, coin, amt)
}

method bankBurnTokens(
    bank: Ref,
    from: AccountID,
    path: Path,
    coin: Coin,
    amt: Int,
    ghostPerm: Ref
) returns (success: Bool)
    requires Bank(bank)
    requires acc(ghostPerm.actionPerm)
    requires canMint(ghostPerm.actionPerm, toResourceHolder(from), toResource(coin), 0 - amt)
    ensures Bank(bank)
    ensures !success ==> bank.bankBalances == old(bank.bankBalances)
    ensures success ==> balanceOf(bank.bankBalances, from, coin) == old(balanceOf(bank.bankBalances, from, coin) - amt)
{
    if(hasCoin(bank.bankBalances, from, coin) && 
       path in bank.bankBalances[from][coin] && 
       bank.bankBalances[from][coin][path] >= amt) {
        ghostMint(
            toResourceHolder(from),
            toResource(coin),
            0 - amt,
            ghostPerm
        )
        updateBalance(bank, from, path, coin, 0 - amt)
        success := true
    } else {
        success := false
    }
}

method bankTransferTokens(
    bank: Ref,
    from: AccountID,
    to: AccountID,
    path: Path,
    coin: Coin,
    amt: Int,
    ghostPerm: Ref
) returns (success: Bool)
    requires Bank(bank)
    requires amt >= 0
    requires acc(ghostPerm.actionPerm)
    requires canTransferOut(ghostPerm.actionPerm, toResourceHolder(from), toResource(coin), amt)
    ensures Bank(bank)
    ensures !success ==> bank.bankBalances == old(bank.bankBalances)
    ensures success ==> balanceOf(bank.bankBalances, from, coin) == old(balanceOf(bank.bankBalances, from, coin) - amt)
    ensures success ==> balanceOf(bank.bankBalances, to, coin) == old(balanceOf(bank.bankBalances, to, coin) + amt)
{
    if(from != to && hasCoin(bank.bankBalances, from, coin) && 
       path in bank.bankBalances[from][coin] && 
       bank.bankBalances[from][coin][path] >= amt) {
       ghostTransfer(
            toResourceHolder(from),
            toResourceHolder(to),
            toResource(coin),
            amt,
            ghostPerm
        )
        updateBalance(bank, from, path, coin, (0 - amt))
        updateBalance(bank, to, path, coin, amt)
        success := true
    } else {
        success := false
    }
}

domain Port {

}

domain Channel {

}

function escrowAddress(bank: Ref, channel: Channel): AccountID

method sendFungibleTokens(
    bank: Ref,
    path: Path,
    coin: Coin,
    amount: Int,
    sender: AccountID,
    receiver: AccountID,
    sourcePort: Port,
    sourceChannel: Channel,
    ghostPerm: Ref
) returns (success: Bool)
    requires Bank(bank)
    requires amount >= 0
    requires acc(ghostPerm.actionPerm)
    requires !startsWith(path, sourcePort, sourceChannel) ==> 
        canTransferOut(ghostPerm.actionPerm, toResourceHolder(sender), toResource(coin), amount)
    requires startsWith(path, sourcePort, sourceChannel) ==> 
        canBurn(ghostPerm.actionPerm, toResourceHolder(sender), toResource(coin), amount)
    ensures Bank(bank)
    ensures !success ==> bank.bankBalances == old(bank.bankBalances)
{
    if(!startsWith(path, sourcePort, sourceChannel)) {
        success := bankTransferTokens(
            bank,
            sender,
            escrowAddress(bank, sourceChannel),
            path,
            coin,
            amount,
            ghostPerm
        )
    } else {
        success := bankBurnTokens(
            bank,
            sender,
            path,
            coin,
            amount,
            ghostPerm
        )
    }
}

method onRecvPacket(
    bank: Ref,
    path: Path,
    sourcePort: Port,
    sourceChannel: Channel,
    destChannel: Channel,
    coin: Coin,
    amt: Int,
    receiver: AccountID,
    ghostPerm: Ref
) returns (success: Bool) 
    requires Bank(bank)
    requires amt >= 0
    requires acc(ghostPerm.actionPerm)
    requires startsWith(path, sourcePort, sourceChannel) ==> 
        canTransferOut(ghostPerm.actionPerm, toResourceHolder(escrowAddress(bank, destChannel)), toResource(coin), amt)
    requires !startsWith(path, sourcePort, sourceChannel) ==> 
        canMint(ghostPerm.actionPerm, toResourceHolder(receiver), toResource(coin), amt)
    ensures Bank(bank)
{
    if(startsWith(path, sourcePort, sourceChannel)) {
        success := bankTransferTokens(
            bank,
            escrowAddress(bank, destChannel),
            receiver,
            dropPrefix(path, sourcePort, sourceChannel),
            coin,
            amt,
            ghostPerm
        )
    } else {
        success := bankMintTokens(
            bank,
            receiver,
            prependPrefix(path, sourcePort, sourceChannel),
            coin,
            amt,
            ghostPerm
        )
    }
}